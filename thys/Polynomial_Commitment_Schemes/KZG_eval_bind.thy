theory KZG_eval_bind

imports KZG_correct "tSDH_assumption" CryptHOL_ext

begin

section \<open>Evaluation Binding of the KZG\<close>

text \<open>In this theory we prove that the KZG is evaluation binding. The proof is a reduction to the
t-strong Diffie-Hellman assumption and follows the paper proof by Kate, Zaverucha, and Goldberg in
the extended version of ``Constant-Size Commitments to Polynomials and Their Applications''
\<^cite>\<open>KZG10\<close>.\<close>

text \<open>We prove evaluation binding from the abstract PCS for the KZG instantiated as a PCS\<close>

locale KZG_PCS_binding = KZG_PCS_correct
begin 

subsection \<open>t-SDH game\<close> 

text \<open>We instantiate the t-SDH game for the group Gp\<close>
sublocale t_SDH_G\<^sub>p: t_SDH G\<^sub>p max_deg "of_int_mod_ring \<circ> int" "pow_mod_ring G\<^sub>p"
  unfolding t_SDH_def 
  by (rule G\<^sub>p.cyclic_group_axioms)

subsection \<open>Defining a reduction adversary from evaluation binding to t-SDH\<close>

text \<open>The reduction function takes a adversary for the evaluation binding game and returns an 
adversary for the t-SDH game. Specifically, the reduction uses the evaluation binding adversary to 
construct a winning strategy for the t-SDH game (i.e. to win it every time).
Essentially, it uses the fact that the values supplied by the adversary already break the t-SDH 
assumption.\<close>
fun eval_bind_reduction
  :: "('a ck, 'a commit, 'e mod_ring, 'e mod_ring, 'a witness) eval_bind_adversary \<Rightarrow> ('a,'e) t_SDH.adversary"                     
where
  "eval_bind_reduction \<A> PK = do {
  (C, i, \<phi>_of_i, w_i, \<phi>'_of_i, w'_i) \<leftarrow> \<A> PK;
  return_spmf (-i::'e mod_ring, (w_i \<div>\<^bsub>G\<^sub>p\<^esub> w'_i) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i)) )}"



subsection \<open>Helping definitions\<close>

text \<open>The eval\_bind reduction adversary extended for asserts that 
are present in the evaluation binding game. We use this definition to show equivalence to 
the evaluation binding  game. Later on we can then easily over-estimate the probability 
from this extended version to the normal reduction.\<close>
fun ext_eval_bind_reduction
  :: "('a ck, 'a commit, 'e mod_ring, 'e mod_ring, 'a witness) eval_bind_adversary \<Rightarrow> ('a,'e) t_SDH.adversary"                     
where
  "ext_eval_bind_reduction \<A> PK = do {
  (C, i, v, w, v', w') \<leftarrow> \<A> PK;
  _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval PK C i (v,w)
                            \<and> verify_eval PK C i (v',w')); 
  return_spmf (-i::'e mod_ring, (w \<div>\<^bsub>G\<^sub>p\<^esub> w') ^\<^bsub>G\<^sub>p\<^esub> (1 / (v' - v)) )}"

text \<open>show that VerifyEval on two evaluations, \<open>\<phi>_of_i\<close> and \<open>\<phi>'_of_i\<close>, for the same point i, implies
that the t-SDH is broken.
This lemma captures that the adversaries messages already break the t-SDH assumption.\<close>
lemma two_eval_verify_imp_tSDH_break: 
  assumes "\<phi>_of_i \<noteq> \<phi>'_of_i \<and> w_i \<noteq> w'_i 
        \<and> w_i \<in> carrier G\<^sub>p \<and>  w'_i \<in> carrier G\<^sub>p
        \<and> verify_eval ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<alpha>)^t)) [0..<max_deg+1])) C i (\<phi>_of_i, w_i) 
        \<and> verify_eval ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<alpha>)^t)) [0..<max_deg+1])) C i (\<phi>'_of_i, w'_i)"
  shows "\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (1/(\<alpha> + (-i))) 
                              = (w_i \<div>\<^bsub>G\<^sub>p\<^esub> w'_i) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i))"
proof -
  let ?PK = "\<lambda>\<alpha>. (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<alpha>)^t)) [0..<max_deg+1])"
  obtain \<psi>\<^sub>i where \<psi>\<^sub>i: "w_i = \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<psi>\<^sub>i"
    by (metis G\<^sub>p.generatorE assms g_pow_to_int_mod_ring_of_int_mod_ring int_pow_int)
  obtain \<psi>\<^sub>i' where \<psi>\<^sub>i': "w'_i = \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<psi>\<^sub>i'"
    by (metis G\<^sub>p.generatorE assms g_pow_to_int_mod_ring_of_int_mod_ring int_pow_int)

  text \<open>the proof is essentially rearranging equations, an outline can be found in the 
  evaluation binding proof section in the thesis paper.\<close> 
  have "e w_i ((\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<alpha>)  \<div>\<^bsub>G\<^sub>p\<^esub> (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> i)) \<otimes>\<^bsub>G\<^sub>T\<^esub> ((e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> \<phi>_of_i)
      = e w'_i ((\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<alpha>)  \<div>\<^bsub>G\<^sub>p\<^esub> (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> i)) \<otimes>\<^bsub>G\<^sub>T\<^esub> ((e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> \<phi>'_of_i)"
  proof -
    have "map (\<lambda>t. \<^bold>g ^ \<alpha> ^ t) [0..<max_deg + 1] ! 1 =  \<^bold>g ^ \<alpha>"
      by (metis PK_i Suc_le_eq d_pos numeral_nat(7) power_one_right)
    then show ?thesis using assms unfolding verify_eval_def by simp
  qed
  then have "e w_i (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>-i)) \<otimes>\<^bsub>G\<^sub>T\<^esub> ((e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> \<phi>_of_i)
      = e w'_i (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>-i)) \<otimes>\<^bsub>G\<^sub>T\<^esub> ((e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> \<phi>'_of_i)"
    using mod_ring_pow_mult_inv_G\<^sub>p by presburger
  then have "e (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<psi>\<^sub>i) (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>-i)) \<otimes>\<^bsub>G\<^sub>T\<^esub> ((e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> \<phi>_of_i)
      = e (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<psi>\<^sub>i') (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>-i)) \<otimes>\<^bsub>G\<^sub>T\<^esub> ((e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> \<phi>'_of_i)"
    using \<psi>\<^sub>i \<psi>\<^sub>i' by fast
  then have "(e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> (\<psi>\<^sub>i * (\<alpha>-i) + \<phi>_of_i)
      = (e \<^bold>g\<^bsub>G\<^sub>p\<^esub> \<^bold>g\<^bsub>G\<^sub>p\<^esub>) ^\<^bsub>G\<^sub>T\<^esub> (\<psi>\<^sub>i' * (\<alpha>-i) + \<phi>'_of_i)"
    using e_bilinear
    by (metis G\<^sub>p.generator_closed addition_in_exponents_on_e)
  then have "\<psi>\<^sub>i * (\<alpha>-i) + \<phi>_of_i = \<psi>\<^sub>i' * (\<alpha>-i) + \<phi>'_of_i"
    using pow_on_eq_card_GT_carrier_ext'
    by blast
  then have "(\<psi>\<^sub>i - \<psi>\<^sub>i') * (\<alpha>-i) = \<phi>'_of_i - \<phi>_of_i"
    by (simp add: algebra_simps)
  then have "(\<psi>\<^sub>i - \<psi>\<^sub>i')/(\<phi>'_of_i - \<phi>_of_i) = 1/(\<alpha>-i)"
    by (metis \<psi>\<^sub>i \<psi>\<^sub>i' assms divide_divide_eq_left divide_self_if eq_iff_diff_eq_0)
  moreover have "(w_i \<div>\<^bsub>G\<^sub>p\<^esub> w'_i) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i)) = \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<psi>\<^sub>i - \<psi>\<^sub>i')/(\<phi>'_of_i - \<phi>_of_i))"
  proof -
    have "(w_i \<div>\<^bsub>G\<^sub>p\<^esub> w'_i) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i)) 
        = ((\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<psi>\<^sub>i) \<div>\<^bsub>G\<^sub>p\<^esub> (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<psi>\<^sub>i')) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i))"
      using \<psi>\<^sub>i \<psi>\<^sub>i' by fast
    also have "\<dots> = (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<psi>\<^sub>i - \<psi>\<^sub>i')) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i))"
      using mod_ring_pow_mult_inv_G\<^sub>p by presburger
    also have "\<dots> = \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<psi>\<^sub>i - \<psi>\<^sub>i')/(\<phi>'_of_i - \<phi>_of_i))"
      by (metis mod_ring_pow_pow_G\<^sub>p times_divide_eq_right verit_prod_simplify(2))
    finally show ?thesis .
  qed
  ultimately show ?thesis by fastforce
qed

subsubsection \<open>Literal helping lemmas\<close>

text \<open>CryptHOL has some difficulties with simplifying, thus we need to use literal helping lemmas, 
that state the equalities we want to exchange literally.\<close>

lemma add_witness_neq_if_eval_neq: "v \<noteq> v'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v,w) 
                            \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v',w') 
                        \<longleftrightarrow>                                       
                            v \<noteq> v' \<and> w\<noteq> w'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v,w) 
                            \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v',w')"
proof 
  assume asm: "v \<noteq> v'
              \<and> valid_argument i
              \<and> valid_eval (v,w)
              \<and> valid_eval (v',w')
              \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v,w) 
              \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v',w') "
  have "w \<noteq> w'"
  proof -
    obtain w_pow where w_i_pow: "w = \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> w_pow" 
    proof -
      have "w \<in> carrier G\<^sub>p"
        using asm by (simp add: valid_eval_def)
      then show "(\<And>w_pow. w = \<^bold>g ^ w_pow \<Longrightarrow> thesis) \<Longrightarrow> thesis"
        by (metis G\<^sub>p.generatorE g_pow_to_int_mod_ring_of_int_mod_ring int_pow_int)
    qed 
    obtain w'_pow where w'_i_pow: "w' = \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> w'_pow" 
    proof -
      have "w' \<in> carrier G\<^sub>p"
        using asm by (simp add: valid_eval_def)
      then show "(\<And>w_pow. w' = \<^bold>g ^ w_pow \<Longrightarrow> thesis) \<Longrightarrow> thesis"
        by (metis G\<^sub>p.generatorE g_pow_to_int_mod_ring_of_int_mod_ring int_pow_int)
    qed 

    from asm
    have "e w ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) ! 1 \<otimes> inv (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> i)) 
          \<otimes>\<^bsub>G\<^sub>T\<^esub> e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> v 
        =e w' ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) ! 1 \<otimes> inv (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> i)) 
          \<otimes>\<^bsub>G\<^sub>T\<^esub> e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> v' " unfolding verify_eval_def by force
    then have "e (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> w_pow) ((\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<alpha>) \<otimes> inv (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> i)) 
          \<otimes>\<^bsub>G\<^sub>T\<^esub> e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> v 
        =e (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> w'_pow) ((\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> \<alpha>) \<otimes> inv (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> i)) 
          \<otimes>\<^bsub>G\<^sub>T\<^esub> e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> v'"
      using PK_i w_i_pow w'_i_pow
      using add.commute add_diff_cancel_right' d_pos landau_product_preprocess(52) length_upt less_diff_conv nth_map nth_upt power_one_right
      by auto
    then have "e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> (w_pow * (\<alpha> - i)) 
          \<otimes>\<^bsub>G\<^sub>T\<^esub> e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> v 
        =e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> (w'_pow * (\<alpha> - i))
          \<otimes>\<^bsub>G\<^sub>T\<^esub> e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> v'"
      by (metis G\<^sub>p.generator_closed e_bilinear mod_ring_pow_mult_inv_G\<^sub>p)
    then have "e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> (w_pow * (\<alpha> - i) + v)
        =e \<^bold>g \<^bold>g ^\<^bsub>G\<^sub>T\<^esub> (w'_pow * (\<alpha> - i) + v')"
      using mod_ring_pow_mult_G\<^sub>T by auto
    then have "w_pow * (\<alpha> - i) + v = w'_pow * (\<alpha> - i) + v'"
      using pow_on_eq_card_GT_carrier_ext' by blast
    then have "w_pow \<noteq> w'_pow" using asm by force
    
    then show ?thesis 
      using w_i_pow w'_i_pow pow_on_eq_card by presburger
  qed
  then show "v \<noteq> v' \<and> w\<noteq> w'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v,w) 
                            \<and> verify_eval (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1]) C i (v',w')"
    using asm by fast
qed fast

lemma helping_1: "\<phi>_of_i \<noteq> \<phi>'_of_i \<and> w_i \<noteq> w'_i 
        \<and> valid_argument i
        \<and> w_i \<in> carrier G\<^sub>p \<and>  w'_i \<in> carrier G\<^sub>p
        \<and> verify_eval ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<alpha>)^t)) [0..<max_deg+1])) C i (\<phi>_of_i, w_i) 
        \<and> verify_eval ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<alpha>)^t)) [0..<max_deg+1])) C i (\<phi>'_of_i, w'_i)
        \<and> \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (1/(\<alpha> + (-i))) 
        = (w_i \<div>\<^bsub>G\<^sub>p\<^esub> w'_i) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i)) 
  \<longleftrightarrow> 
        \<phi>_of_i \<noteq> \<phi>'_of_i \<and> w_i \<noteq> w'_i 
        \<and> valid_argument i
        \<and> w_i \<in> carrier G\<^sub>p \<and>  w'_i \<in> carrier G\<^sub>p
        \<and> verify_eval ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<alpha>)^t)) [0..<max_deg+1])) C i (\<phi>_of_i, w_i) 
        \<and> verify_eval ((map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((\<alpha>)^t)) [0..<max_deg+1])) C i (\<phi>'_of_i, w'_i)"
  using two_eval_verify_imp_tSDH_break unfolding valid_eval_def by meson


subsection \<open>Proof\<close>

lemma valid_eval_in_carrier[simp]: "valid_eval (v,w) \<equiv> w \<in> carrier G\<^sub>p"
  unfolding valid_eval_def by force


theorem eval_bind_game_eq_t_SDH_strong_ext_red:
  shows "eval_bind_game \<A> = t_SDH_G\<^sub>p.game (ext_eval_bind_reduction \<A>)"
proof -
  note [simp] = Let_def split_def

  text \<open>abbreviations for the mod\_ring version of sample uniform nat 
  and the public key\<close>
  let ?\<alpha> = "\<lambda>\<alpha>. (of_int_mod_ring (int \<alpha>)::'e mod_ring)"
  let ?PK = "\<lambda>\<alpha>. (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((?\<alpha> \<alpha>)^t)) [0..<max_deg+1])"

  have "t_SDH_G\<^sub>p.game (ext_eval_bind_reduction \<A>) = TRY do { 
     \<alpha> \<leftarrow> sample_uniform (order G\<^sub>p);
    (C, i, v, w, v', w') \<leftarrow> \<A> (?PK \<alpha>);
    _ :: unit \<leftarrow> assert_spmf (v \<noteq> v' 
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (?PK \<alpha>) C i (v,w)
                            \<and> verify_eval (?PK \<alpha>) C i (v',w'));
    _::unit \<leftarrow> assert_spmf (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (1/(?\<alpha> \<alpha> + (-i))) = (w \<div>\<^bsub>G\<^sub>p\<^esub> w') ^\<^bsub>G\<^sub>p\<^esub> (1 / (v' - v)));
    return_spmf True 
  } ELSE return_spmf False"
    by (force simp add: t_SDH_G\<^sub>p.game_alt_def[of "(ext_eval_bind_reduction \<A>)"])
  text \<open>Add the fact that witnesses have to be unequal if evaluations are unequal for a easier 
        proof.\<close>
  also have "\<dots> =  TRY do { 
     \<alpha> \<leftarrow> sample_uniform (order G\<^sub>p);
    (C, i, v, w, v', w') \<leftarrow> \<A> (?PK \<alpha>);
    _ :: unit \<leftarrow> assert_spmf (v \<noteq> v' \<and> w \<noteq> w'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (?PK \<alpha>) C i (v,w)
                            \<and> verify_eval (?PK \<alpha>) C i (v',w'));
    _::unit \<leftarrow> assert_spmf (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (1/(?\<alpha> \<alpha> + (-i))) = (w \<div>\<^bsub>G\<^sub>p\<^esub> w') ^\<^bsub>G\<^sub>p\<^esub> (1 / (v' - v)));
    return_spmf True 
  } ELSE return_spmf False"
    using add_witness_neq_if_eval_neq by presburger
  text \<open>Goal is to erase the second assert statement, such that we just end up with the 
  evaluation\_game. To do that, we first merge the two asserts and show that the first assert's 
  statement implies the second one's statement, hence we can leave the second assert's statement 
  out and are left with only the first assert statement.\<close>
 also have "\<dots> = TRY do { 
     \<alpha> \<leftarrow> sample_uniform (order G\<^sub>p);
    (C, i, v, w, v', w') \<leftarrow> \<A> (?PK \<alpha>);
    _ :: unit \<leftarrow> assert_spmf (v \<noteq> v' \<and> w \<noteq> w'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (?PK \<alpha>) C i (v,w)
                            \<and> verify_eval (?PK \<alpha>) C i (v',w')
                            \<and> \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (1/(?\<alpha> \<alpha> + (-i))) 
                              = (w \<div>\<^bsub>G\<^sub>p\<^esub> w') ^\<^bsub>G\<^sub>p\<^esub> (1 / (v' - v)));
    return_spmf True 
  } ELSE return_spmf False"  
   by (simp add: assert_collapse)
  text \<open>We use the equivalence to erase the assert-term that t-SDH is broken, as it is not 
  contained in the evaluation binding game.\<close>
  also have "\<dots> = TRY do { 
     \<alpha> \<leftarrow> sample_uniform (order G\<^sub>p);
    (C, i, v, w, v', w') \<leftarrow> \<A> (?PK \<alpha>);
    _ :: unit \<leftarrow> assert_spmf (v \<noteq> v' \<and> w \<noteq> w'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (?PK \<alpha>) C i (v,w)
                            \<and> verify_eval (?PK \<alpha>) C i (v',w'));
    return_spmf True 
  } ELSE return_spmf False"  
   using helping_1 unfolding valid_eval_in_carrier
   by algebra
 text \<open>remove additional assert-term about the witnesses inequality\<close>
 also have "\<dots> = TRY do { 
     \<alpha> \<leftarrow> sample_uniform (order G\<^sub>p);
    (C, i, v, w, v', w') \<leftarrow> \<A> (?PK \<alpha>);
    _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (?PK \<alpha>) C i (v,w)
                            \<and> verify_eval (?PK \<alpha>) C i (v',w'));
    return_spmf True 
  } ELSE return_spmf False"  
   using add_witness_neq_if_eval_neq[symmetric] by algebra
  text \<open>form the KeyGen function from the uniformly sampled alpha\<close>
  also have "\<dots> = TRY do { 
    (PK,PK') \<leftarrow> key_gen;
    (C, i, v, w, v', w') \<leftarrow> \<A> PK;
    _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval PK C i (v,w)
                            \<and> verify_eval PK C i (v',w'));
    return_spmf True 
  } ELSE return_spmf False"
    unfolding key_gen_def Setup_def by simp
  text \<open>split the accumulated assert, to obtain the sequence in the evaluation binding game\<close>
  also have "\<dots> = TRY do {
  (PK,_) \<leftarrow> key_gen;
  TRY do {
      (C, i, v, w, v', w') \<leftarrow> \<A> PK;
      TRY do {
      _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval PK C i (v,w)
                            \<and> verify_eval PK C i (v',w'));
      return_spmf True
      } ELSE return_spmf False 
    } ELSE return_spmf False 
  } ELSE return_spmf False"
  unfolding split_def Let_def
   by(fold try_bind_spmf_lossless2[OF lossless_return_spmf]) simp
  also  have "\<dots> = TRY do {
  (PK,_) \<leftarrow> key_gen;
    TRY do {
    (C, i, v, w, v', w') \<leftarrow> \<A> PK;
      TRY do {
        _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'\<and> valid_argument i \<and> valid_eval (v,w) \<and> valid_eval (v',w'));
        _ :: unit \<leftarrow> assert_spmf (verify_eval PK C i (v,w) \<and> verify_eval PK C i (v',w'));
        return_spmf True
      } ELSE return_spmf False 
    } ELSE return_spmf False 
  } ELSE return_spmf False"  
    by (simp add: assert_collapse)
  also  have "\<dots> = TRY do {
  (PK,_) \<leftarrow> key_gen;
    TRY do {
    (C, i, v, w, v', w') \<leftarrow> \<A> PK;
      TRY do {
        _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'\<and> valid_argument i \<and> valid_eval (v,w) \<and> valid_eval (v',w'));
        TRY do {
          let b = verify_eval PK C i (v,w);
          let b' = verify_eval PK C i (v',w');
          return_spmf (b \<and> b')
        } ELSE return_spmf False
      } ELSE return_spmf False 
    } ELSE return_spmf False 
  } ELSE return_spmf False" 
   by(auto simp add: try_bind_assert_spmf try_spmf_return_spmf1 intro!: try_spmf_cong bind_spmf_cong)
  also have "\<dots> = TRY do {
  (PK,_) \<leftarrow> key_gen;
  (C, i, v, w, v', w') \<leftarrow> \<A> PK;
  _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'\<and> valid_argument i \<and> valid_eval (v,w) \<and> valid_eval (v',w'));
  let b = verify_eval PK C i (v,w);
  let b' = verify_eval PK C i (v',w');
  return_spmf (b \<and> b')} ELSE return_spmf False"
   unfolding split_def Let_def
   by(fold try_bind_spmf_lossless2[OF lossless_return_spmf]) simp
  also have "\<dots>= eval_bind_game \<A>"
     using eval_bind_game_def unfolding key_gen_def Setup_def by auto
  finally show ?thesis  ..
qed

lemma overestimate_reductions: "spmf (t_SDH_G\<^sub>p.game (ext_eval_bind_reduction \<A>)) True 
  \<le> spmf (t_SDH_G\<^sub>p.game (eval_bind_reduction \<A>)) True"
  (is "spmf ?lhgame True \<le> spmf ?rhgame True")
proof -
  note [simp] = Let_def split_def

  text \<open>abbreviations for the mod\_ring version of sample uniform nat 
  and the public key\<close>
  let ?\<alpha> = "\<lambda>\<alpha>. (of_int_mod_ring (int \<alpha>)::'e mod_ring)"
  let ?PK = "\<lambda>\<alpha>. (map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> ((?\<alpha> \<alpha>)^t)) [0..<max_deg+1])"

  text \<open>We extend the t-SDH game with reduction adversary to a complete game.\<close>
  have bind_red_ext: "t_SDH_G\<^sub>p.game (ext_eval_bind_reduction \<A>) = TRY do { 
     \<alpha> \<leftarrow> sample_uniform (order G\<^sub>p);
      (C, i, v, w, v', w') \<leftarrow> \<A> (?PK \<alpha>);
      _ :: unit \<leftarrow> assert_spmf (v \<noteq> v'
                            \<and> valid_argument i
                            \<and> valid_eval (v,w)
                            \<and> valid_eval (v',w')
                            \<and> verify_eval (?PK \<alpha>) C i (v,w)
                            \<and> verify_eval (?PK \<alpha>) C i (v',w')); 
    _::unit \<leftarrow> assert_spmf (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (1/(?\<alpha> \<alpha> + (-i))) = (w \<div>\<^bsub>G\<^sub>p\<^esub> w') ^\<^bsub>G\<^sub>p\<^esub> (1 / (v' - v)));
    return_spmf True 
  } ELSE return_spmf False"
    by (force simp add: t_SDH_G\<^sub>p.game_alt_def[of "(ext_eval_bind_reduction \<A>)"])

  text \<open>We extend the t-SDH game with reduction adversary to a complete game.\<close>
  have eval_bind_red_ext: "t_SDH_G\<^sub>p.game (eval_bind_reduction \<A>) = TRY do { 
     \<alpha> \<leftarrow> sample_uniform (order G\<^sub>p);
    (C, i, \<phi>_of_i, w_i, \<phi>'_of_i, w'_i) \<leftarrow> \<A> (?PK \<alpha>);
    _::unit \<leftarrow> assert_spmf (\<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (1/(?\<alpha> \<alpha> + (-i))) = (w_i \<div>\<^bsub>G\<^sub>p\<^esub> w'_i) ^\<^bsub>G\<^sub>p\<^esub> (1 / (\<phi>'_of_i - \<phi>_of_i)));
    return_spmf True 
  } ELSE return_spmf False"
    by (force simp add: t_SDH_G\<^sub>p.game_alt_def[of "(eval_bind_reduction \<A>)"])

  text \<open>We show the thesis in ennreal, which implies the plain thesis\<close>
  have "ennreal (spmf (t_SDH_G\<^sub>p.game (ext_eval_bind_reduction \<A>)) True) 
    \<le> ennreal (spmf (t_SDH_G\<^sub>p.game (eval_bind_reduction \<A>)) True)"
    unfolding bind_red_ext eval_bind_red_ext
    apply (simp add: spmf_try_spmf ennreal_spmf_bind)
    apply (rule nn_integral_mono)+
    apply (simp add: assert_spmf_def)
    apply (simp add: measure_spmf.emeasure_eq_measure)
    done

  then show ?thesis by simp
qed

text \<open>Finally we put everything together: 
we conclude that for every efficient adversary the advantage of winning the 
evaluation binding game is less equal to breaking the t-SDH assumption.\<close>
theorem evaluation_binding: "eval_bind_advantage \<A> \<le> t_SDH_G\<^sub>p.advantage (eval_bind_reduction \<A>)"
  using  overestimate_reductions
  unfolding t_SDH_G\<^sub>p.advantage_def eval_bind_advantage_def eval_bind_game_eq_t_SDH_strong_ext_red
  by fast

end

end