theory BatchKZG_correct
  imports BatchKZG_def
begin

section \<open>Correctness of the batched KZG\<close>

locale BatchEvalKZG_PCS_correct = BatchEvalKZG + KZG_PCS_correct
begin 

text \<open>We show perfect correctness 
i.e. that the game played by an honest committer and honest verifier has guaranteed success; 
success probability 1.\<close>

text \<open>We show the pairing check performed by VerifyEvalVBatch by values (e.g. with $g^{\phi(\alpha)}$ instead 
of the commitment C). This enables us to prove that the pairing check holds for e.g. a correctly 
computed commitment.\<close>
lemma eq_on_e_Batch: "(e (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (\<Prod>i\<in>B. [:- i, 1:]) \<alpha>) (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (\<psi>\<^sub>B B \<phi>) \<alpha>) 
  \<otimes>\<^bsub>G\<^sub>T\<^esub> (e \<^bold>g (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (r B \<phi>) \<alpha>)) 
  = e (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly \<phi> \<alpha>) \<^bold>g)"
proof -
  have "(poly (\<Prod>i\<in>B. [:- i, 1:]) \<alpha>) * ( poly (\<psi>\<^sub>B B \<phi>) \<alpha>) + poly (r B \<phi>) \<alpha> = poly \<phi> \<alpha>"
    by (metis (no_types, lifting) \<psi>\<^sub>B.simps add.commute add_diff_cancel_right' div_poly_eq_0_iff minus_mod_eq_mult_div mod_div_mult_eq nonzero_mult_div_cancel_left poly_hom.hom_add poly_mult r.elims)
  then show ?thesis 
    using e_bilinear e_linear_in_fst e_linear_in_snd G\<^sub>p.generator_closed addition_in_exponents_on_e by presburger
qed

theorem KZG_correct: "bKZG.correct_eval"
  unfolding bKZG.correct_eval_def valid_poly_def valid_argument_batch_def
proof (intro allI, intro impI)
  fix \<phi>::"'e mod_ring poly"
  fix B :: "'e mod_ring set"
  assume deg_\<phi>: "degree \<phi> \<le> max_deg"
  assume cardB: "card B \<le> max_deg"
  show "spmf (bKZG.correct_eval_game \<phi> B) True = 1"
  proof -
    text \<open>show that $g^{\psi_B(\alpha)}$ is correctly computed using a correct public key PK\<close>
    have g_pow_\<psi>B: "\<forall>x. g_pow_PK_Prod
                   (map (\<lambda>t. \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> of_int_mod_ring (int x) ^ t) [0..<max_deg + 1])
                   (\<psi>\<^sub>B B \<phi>) =  \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (\<psi>\<^sub>B B \<phi>) (of_int_mod_ring (int x))"
      using deg_\<psi>\<^sub>B g_pow_PK_Prod_correct le_trans deg_\<phi> by blast 
    text \<open>show that $g^{r(\alpha)}$ is correctly computed using a correct public key PK\<close>
    have g_pow_rB: "\<forall>x. g_pow_PK_Prod
                   (map (\<lambda>t. \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> of_int_mod_ring (int x) ^ t) [0..<max_deg + 1])
                   (r B \<phi>) =  \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (r B \<phi>) (of_int_mod_ring (int x))"
      using deg_r g_pow_PK_Prod_correct le_trans deg_\<phi> by blast
    text \<open>show that $g^{\prod_{i \in B}(\alpha - i)}$ is correctly computed using a correct public key PK\<close>
    have g_ow_Prod: "\<forall>x. g_pow_PK_Prod
                   (map (\<lambda>t. \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> of_int_mod_ring (int x) ^ t) [0..<max_deg + 1])
                   (\<Prod>i\<in>B. [:- i, 1:]) =  \<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly  (\<Prod>i\<in>B. [:- i, 1:]) (of_int_mod_ring (int x))"
      using deg_Prod g_pow_PK_Prod_correct le_trans cardB less_imp_le_nat by presburger

    text \<open>unfold Setup to gain access to the definition of the public key PK. This step is necessary 
    to be able to use the conversions showed above\<close>
    have "spmf (bKZG.correct_eval_game \<phi> B) True = 
      spmf (do{
      x :: nat \<leftarrow> sample_uniform (order G\<^sub>p);
      let \<alpha> = of_int_mod_ring (int x);
      let PK = map (\<lambda>t. \<^bold>g\<^bsub>G\<^sub>p\<^esub> ^\<^bsub>G\<^sub>p\<^esub> (\<alpha>^t)) [0..<max_deg+1];
      (C,d) \<leftarrow> commit PK \<phi>;
      let W  = eval_batch PK d \<phi> B;
      return_spmf (verify_eval_batch PK C B W)
      }) True"
      unfolding bKZG.correct_eval_game_def key_gen_def Setup_def
        abstract_polynomial_commitment_scheme.correct_eval_game_def  Let_def
      by force
    text \<open>transform the computation from the functions into values.\<close>
    also have "\<dots> = spmf (do{
      x :: nat \<leftarrow> sample_uniform (order G\<^sub>p);
      return_spmf (
      e (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (\<Prod>i\<in>B. [:- i, 1:]) (of_int_mod_ring (int x))) (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (\<psi>\<^sub>B B \<phi>) (of_int_mod_ring (int x))) 
    \<otimes>\<^bsub>G\<^sub>T\<^esub> (e \<^bold>g (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly (r B \<phi>) (of_int_mod_ring (int x)))) 
    = e (\<^bold>g ^\<^bsub>G\<^sub>p\<^esub> poly \<phi> (of_int_mod_ring (int x))) \<^bold>g)
      }) True"
      unfolding eval_batch_def verify_eval_batch_def commit_def Let_def split_def
        g_pow_PK_Prod_correct[OF deg_\<phi>]
      using g_pow_\<psi>B g_pow_rB g_ow_Prod
      by simp
    text \<open>Use the pairing equality by value showed in 'eq\_on\_e\_Batch' to conclude that the game 
    simply returns True i.e. has a success probability of 1.\<close>
    also have "\<dots> = spmf (do{
      x :: nat \<leftarrow> sample_uniform (order G\<^sub>p);
      return_spmf (True
    )}) True"
      using eq_on_e_Batch deg_Prod by algebra
    also have "\<dots> = spmf (scale_spmf (weight_spmf (sample_uniform (Coset.order G\<^sub>p))) (return_spmf True)) True"
      using bind_spmf_const[of "sample_uniform (Coset.order G\<^sub>p)" "return_spmf True"] by presburger
    also have "\<dots> = 1"
      using weight_sample_uniform_gt_0 CARD_G\<^sub>p p_gr_two by simp
    finally show ?thesis .
  qed
qed

end

end